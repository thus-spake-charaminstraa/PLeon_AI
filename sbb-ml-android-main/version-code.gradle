/*
 * Copyright (C) Schweizerische Bundesbahnen SBB, 2016.
 */

ext {
    /**
     * Generates an Android version code from the version of the project which has the format xx.xx.xxxx.
     * This is designed to handle the -SNAPSHOT and -RC format.
     *
     * I.e. during development the version ends with -SNAPSHOT. As the code stabilizes and release nears,
     * one or many Release Candidates are tagged. These end with "-RC1", "-RC2" etc. The final release is
     * without any suffix.
     *
     * When modifying this code keep in mind that the max value for version code is max int (2'147'483'647).
     *
     * @return version code based on given/configured publish version
     */
    generateVersionCode = {
        // The rules are as follows:
        // -SNAPSHOT counts as 0
        // -RC* counts as the RC number, i.e. 1 to 8 -> only 8 RCs possible, but that should be enough
        // final release counts as 9

        def candidate = "9"
        def (major, minor, patch) = PUBLISH_VERSION.toLowerCase().replaceAll('-', '').tokenize('.')
        if (patch.endsWith("snapshot")) {
            candidate = "0"
            patch = patch.replaceAll("[^0-9]", "")
        } else {
            def rc
            (patch, rc) = patch.tokenize("rc")
            if (rc) {
                candidate = rc
            }
        }

        (major, minor, patch, candidate) = [major, minor, patch, candidate].collect { it.toInteger() }

        (major * 10_000_000) + (minor * 100_000) + (patch * 10) + candidate
    }
}